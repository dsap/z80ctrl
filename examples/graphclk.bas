5 REM Graphical clock for MSX-BASIC
10 FG=15: BG=4: REM foreground/background colors
20 CX=128: CY=96: R=95: REM center x/y coord and radius
30 SR=R*0.72: MR=R*0.60: HR=R*0.50
50 S=99: M=99: H=99
60 SX=CX: SY=CY: MX=CX: MY=CY: HX=CX: HY=CY
70 DEF FNBCD(V)=(V AND &HF0)/16*10+(V AND &HF)
80 SCREEN 2: COLOR FG,BG,BG: CLS
90 CIRCLE(CX,CY),R
100 OUT 0,0: REM select RTC chip
110 GOSUB 500
120 IF INKEY$="" THEN 110
130 END

500 REM Update second hand
510 LS=S: OUT 1,0: S=FNBCD(INP(2))
520 IF S=LS THEN RETURN
530 LX=SX:LY=SY
540 SA=S/60*6.28-1.57: SX=CX+COS(SA)*SR: SY=CY+SIN(SA)*SR
550 GOSUB 800

600 REM Update minute hand
610 LM=M: OUT 1,1: M=FNBCD(INP(2))
620 IF M=LM THEN RETURN
630 LX=MX:LY=MY
640 MA=M/60*6.28-1.57: MX=CX+COS(MA)*MR: MY=CY+SIN(MA)*MR
650 GOSUB 800

700 REM Update hour hand
710 LH=H: OUT 1,2: H=INP(2)
720 IF (H AND &H40) THEN H=H AND &H1F
730 H=H AND &H3F: H=FNBCD(H)
740 IF H > 12 THEN H=H-12
750 IF H=LH THEN RETURN
760 LX=HX:LY=HY
770 HA=H/12*6.28-1.57: HX=CX+COS(HA)*HR: HY=CY+SIN(HA)*HR
780 GOSUB 800
790 RETURN

800 REM Draw clock face
810 COLOR BG
820 LINE(CX,CY)-(LX,LY)
830 COLOR FG
840 LINE(CX,CY)-(SX,SY)
850 LINE(CX,CY)-(MX,MY)
860 LINE(CX,CY)-(HX,HY)
870 RETURN